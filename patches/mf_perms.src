// 
// Perms is a patch to secure filesystem on Mainframe to enhance user security a bit
// Hooks changePermissions() into cmdloop(), cmdPreHandlers() and cmdPostHandlers()
if get_custom_object.indexes.indexOf("Marinette") == null then exit
Marinette = get_custom_object.Marinette




//////////////////////////////////////////////////////////// 
// Patch functionality
////////////////////////////////////////////////////////////
changePermissions = function
    computer = Marinette.Mainframe.shell.host_computer
    fd = computer.File("/")

    results = [
        fd.chmod("u-wrx", true),
        fd.chmod("g-wrx", true),
        fd.chmod("o-wrx", true),
        fd.set_group("root", true),
        fd.set_owner("root", true),
    ]

    for result in results
        if result == "" then continue
        print("<color=red><b>Perms</b>: COULD NOT SECURE MAINFRAME FILESYSTEM!!!")
        break
    end for
end function




//////////////////////////////////////////////////////////// 
// Hooks
////////////////////////////////////////////////////////////

// 
// Run changePermissions() when Marinette initialized and is in cmdloop
_CMDLOOP = @Marinette["Console"]["cmdloop"]
Marinette.Console.cmdloop = function
    changePermissions
    _CMDLOOP
end function

// 
// Run changePermissions() before command execution
_CMD_PRE_HANDLERS = @Marinette["RuntimeHelpers"]["cmdPreHandlers"]
Marinette.RuntimeHelpers.cmdPreHandlers = function
    _CMD_PRE_HANDLERS
    changePermissions
end function

// 
// Run changePermissions() after command execution
_CMD_POST_HANDLERS = @Marinette["RuntimeHelpers"]["cmdPostHandlers"]
Marinette.RuntimeHelpers.cmdPostHandlers = function
    _CMD_POST_HANDLERS
    changePermissions
end function

// 
// Run changePermissions() when a command has not found or when cmd input length is zero
_CMD_FAIL_HANDLERS = @Marinette["RuntimeHelpers"]["cmdFailHandlers"]
Marinette.RuntimeHelpers.cmdFailHandlers = function
    _CMD_FAIL_HANDLERS
    changePermissions
end function
